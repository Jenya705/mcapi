sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

jar {
    baseName("mcapi")
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.contains("mcapi") && it.name.endsWith(".jar") }.collect { zipTree(it) }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = group
            artifactId = "mcapi"
            version = project.version

            from components.java
        }
    }
    repositories {
        maven {
            def snapshotUrl = "https://repo.codemc.io/repository/maven-snapshots/"
            def releaseUrl = "https://repo.codemc.io/repository/maven-releases/"

            // You can use any other check here to set what URL should be used.
            url = project.version.endsWith("alpha") || project.version.endsWith("beta") ? snapshotUrl : releaseUrl

            // ORG_GRADLE_PROJECT_mavenUsername and ORG_GRADLE_PROJECT_mavenPassword are the environments you defined before.
            def mavenUsername = System.getenv("CODEMC_mavenUsername")
            def mavenPassword = System.getenv("CODEMC_mavenPassword")

            if(mavenUsername != null && mavenPassword != null) {
                System.out.println("Setting credentials")
                credentials {
                    username = mavenUsername
                    password = mavenPassword
                }
            }
        }
    }
}

dependencies {
    implementation project(":mcapi-api")
    implementation project(":mcapi-rest")
    implementation 'net.kyori:adventure-text-serializer-gson:4.9.3'
    implementation 'org.slf4j:slf4j-api:1.7.32'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.13.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.1'
    implementation "io.projectreactor.netty:reactor-netty-http:1.0.15"
    implementation 'mysql:mysql-connector-java:8.0.26'
    implementation 'org.xerial:sqlite-jdbc:3.36.0.2'
    implementation 'com.google.inject:guice:5.0.1'
    testImplementation 'com.h2database:h2:2.0.206'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.2.0'
    testImplementation 'ch.qos.logback:logback-classic:1.2.10'
}